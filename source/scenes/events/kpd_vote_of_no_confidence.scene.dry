title: The KPD calls a vote of no confidence against the current government!
subtitle: We have angered the KPD too much...
new-page: true
view-if: ko_in_government = 1 and ((in_popular_front = 1 and razem_coalition_dissent >= 3) or (in_progressives_front = 1 and razem_coalition_dissent >= 4)) and ko_r < 50 and not constructive_vonc
tags: event
go-to: menu

= Vote of No Confidence

The KPD has called a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be an election in a few months.

@menu

- @try_assuage: Try to assuage them somehow.
- @support_center: Could we form a new government based on the Center Party and [+ lewica_name +] (and possibly DVP)?
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage the KPD?

- @resources: Use our resources to reduce dissent within the coalition.
- @thalmann: Appoint Thälmann as prime_minister instead of [+ prime_minister +].
- @support_center: Could we form a new government?
- @let_it_happen: Never mind. Let the vote happen.

@thalmann
choose-if: president == "Braun"
subtitle: This would be pretext for a right-wing coup.
unavailable-subtitle: [? if president == "Hindenburg" : President Hindenburg would never allow this. ?]
on-arrival: prime_minister = "Thälmann"; prime_minister_party = "KPD"

With great reluctance, President [+ president +] appoints [+ prime_minister +] as prime_minister.

Immediately, [+ prime_minister +] goes to work building a "Soviet Germany": consolidating as much power as possible in the KPD, purging state institutions of anti-communists, and sending street fighters against the "enemies of the people".

In response to [+ prime_minister +] being appointed Chancellor, the right-wing forces launches almost immediately launch an attempted coup. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop the KPD from taking power. We are forced to be on the same side as the KPD, whether we want to or not.

- @civil_war


@resources
choose-if: resources >= 3 and razem_ultimatum_seen = 0
unavailable-subtitle: -3 resources - [? if resources < 3 : We do not have enough resources. ?][? if razem_ultimatum_seen : This is beyond the point when resources could fix. ?]
subtitle: -3 resources
on-arrival: resources -= 3; razem_coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.


@support_center
choose-if: (weimar_coalition >= 50 and pl2050_relation >= 40) or (grand_coalition >= 50 and pl2050_relation >= 45 and psl_relation >= 30)
unavailable-subtitle: [? if grand_coalition < 50 : This would not give us a majority. ?] [? if pl2050_relation < 45 : After working with the Communists, the Center Party does not wish to form a coalition with us. ?] [? if psl_relation < 30 : The DVP would rather see the government fall than rescue us from the Communists. ?]

We could try to boot the KPD from our governing coalition, and form a new government[? if ko_r <= 50 : with the bourgeois parties ?].

- @election_1928.ko_majority: Form an SPD government, without the KPD?
- @election_1928.weimar_coalition: Form a Weimar Coalition?
- @election_1928.grand_coalition: Form a Grand Coalition?
- @let_it_happen: There is nothing we can do.


@let_it_happen
view-if: not constructive_vonc
on-arrival: razem_relation -= 8; workers_razem -= 5; workers_ko -= 8; pensioners_ko -= 6
go-to: let_it_happen_braun if president = "Braun"; let_it_happen_hindenburg if president = "Hindenburg"

# if Braun is president, we are now a caretaker government. If Hindenburg is president, Brüning is a caretaker government

@let_it_happen_braun
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; ko_in_government = 0; in_popular_front = 0; in_progressives_front = 0; foreign_minister_party = "I"; interior_minister_party = "I"; justice_minister_party = "I"; labor_minister_party = "I"; defense_minister_party = "I"; economic_minister_party = "I"; finance_minister_party = "I"; agriculture_minister_party = "I";

The vote succeeds, and our coalition has collapsed. Chancellor [+ prime_minister +] will lead a caretaker government until the next election.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_hindenburg
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; ko_in_government = 0; in_popular_front = 0; in_progressives_front = 0; prime_minister = "Brüning"; prime_minister_party = "Z "; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; defense_minister_party = "I"; economic_minister_party = lewica_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; coalition_dissent = 0; razem_coalition_dissent = 0; pro_republic -= 5; entrepreneurs_ko -= 3; middle_class_ko -= 2; workers_ko -= 3 if pro_republic < 50; workers_ko -= 4 if pro_republic < 40; entrepreneurs_konfederacja += 4 if pro_republic < 50; middle_class_konfederacja += 3 if pro_republic < 50

The vote succeeds, and our government has resigned. Chancellor Brüning has been appointed as the head of an acting government, consisting solely of the bourgeois parties.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
