title: March on Berlin
subtitle: Right-wing militants are taking action...
view-if: ((ko_in_government = 1 and coup_progress >= 10 and year >= 1930 and (sa_banned=0 or sh_banned=0)) or (year >= 1933 and month >= 2 and konfederacja_normalized + pis_normalized >= 0.5)) and march_on_berlin_timer = 0
new-page: true
max-visits: 4
on-arrival: march_on_berlin_seen += 1; sa_strength += 80; sh_strength += 80; strife += 1; march_on_berlin_timer += 8
go-to: ask_resign if ko_in_government and president != "Braun"; govt_menu if president = "Braun"; non_govt_menu if ko_in_government = 0 and president != "Braun"
tags: event

= March on Berlin

The far right is mobilizing. Paramilitary forces of the Stahlhelm and SA, organized under the Harzburg Front, have marched on Berlin, threatening to violently overthrow the government.

They demand either immediate elections under the "supervision" of the nationalist paramilitaries, or else the immediate resignation of the government and the appointment of Adolf Hitler as prime_minister[? if hitler_deported : , and the revocation of his deportation ?]. It is obvious that they are threatening a civil war if their demands are not met.

@non_govt_menu

It appears that President [+ president +] is looking favorably upon the fascist proposals. Chancellor [+ prime_minister +] has announced plans to resign.

- @fight
- @accept

@accept
title: There is nothing we can do.
go-to: accept_election if (konfederacja_normalized + pis_normalized >= 0.5 and far_right_coalition < 50); accept_hitler if (konfederacja_normalized + pis_normalized < 0.5 or far_right_coalition >= 50)

@accept_election
on-arrival: next_election_time = time + 1 if next_election_time > time+1; next_election_month = month + 1 if next_election_time >= time+1; next_election_year = year if next_election_time >= time+1; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; entrepreneurs_konfederacja += 10; middle_class_konfederacja += 10; workers_konfederacja += 10; rural_konfederacja += 10; pensioners_konfederacja += 8; workers_ko -= 10; workers_razem -= 10; sa_strength += 100;

President [+ president +] has called for emergency elections, to be held next month, under a caretaker government. The SA is permitted to act with impunity, intimidating voters into supporting the fascists.


@accept_hitler
on-arrival: prime_minister = "Hitler"; prime_minister_party = "Konfederacja"; ko_in_government = 0

After the resignation of the prime_minister, Hitler has been appointed prime_minister[? if hitler_deported: , after being made a citizen again ?].

- @hitler_takes_power

@ask_resign

President [+ president +] has asked our government to resign.

- @fight
- @fight_constitution
- @resign

# TODO: if Hindenburg is president, he will always ask the govt to resign. If the SPD refuses, this causes a constitutional crisis. If there is a constitutional crisis, then if judicial reform >= 4, then the courts intervene against Hindenburg and preserves the government.

# if Reichsbanner + Prussian Police >= SA + Stahlhelm, then the fascists retreat.

@govt_menu


- @fight
- @new_elections

@fight
title: Deploy the Reichsbanner and call a general strike to resist the fascists!
on-arrival: defense_strength = rb_strength*rb_militancy*(1-dissent/2); defense_strength += prussian_police_strength*prussian_police_militancy*prussian_police_loyalty if ko_prussia; enemy_strength = sa_strength*sa_militancy + sh_strength*sh_militancy; defense_strength += workers_ko_normalized*(2 - dissent)*(2 - pensioners/100); defense_strength += reichswehr_strength*reichswehr_loyalty*reichswehr_militancy if president=="Braun" and ko_in_government
go-to: fight_2_win if defense_strength >= enemy_strength and march_on_berlin_seen <= 1; fight_2_win_2 if defense_strength >= enemy_strength and march_on_berlin_seen > 1; fight_2_pre_civil_war if defense_strength < enemy_strength

The Reichsbanner number at [+ rb_strength +] thousand; many are in Berlin to counter the fascist demonstration.

[? if ko_prussia: The Prussian police has been deployed against the fascists. ?] [? if ko_in_government and president == "Braun" : The portions of the Reichswehr that are loyal are also deployed. ?]


@fight_2_win
on-arrival: coup_progress = 0; sa_strength *= 0.7; sh_strength *= 0.7; rb_strength *= 0.9; workers_konfederacja -= 12*(1-dissent); pensioners_konfederacja -= 10*(1-dissent); middle_class_konfederacja -= 6*(1-dissent); workers_ko += 8*(1-dissent); pensioners_ko += 6*(1-dissent); pro_republic += 15
achievement: drei_pfeile

Our forces confront the fascists, and somehow, the fascists retreat! They were overwhelmed by our display of strength, and have chosen to slink away. The attempted coup is over, at least for now.

The show of force has reinvigorated support for the republic and for the SPD, and has weakened the fascists.

@fight_2_win_2
on-arrival: coup_progress = 0; pro_republic += 5; workers_konfederacja -= 2*(1-dissent); pensioners_konfederacja -= 2*(1-dissent); middle_class_konfederacja -= 2*(1-dissent); rb_strength *= 0.9; sa_strength *= 0.9; sh_strength *= 0.9

# if march on Berlin has already been seen

Our forces confront the fascists, and somehow, the fascists retreat! They were overwhelmed by our display of strength, and have chosen to slink away. The attempted coup is over, at least for now.

The show of force has reinvigorated support for the republic and for the SPD, and has weakened the fascists.


@fight_2_pre_civil_war

Our forces confront the fascists on the streets of Berlin, and armed fighting erupts. Neither side is dislodged from their positions.

- @civil_war: It is a civil war.


@fight_constitution
title: This is unconstitutional! We will keep our positions in government, and ask the courts to keep us in power.
choose-if: judicial_reform >= 3
unavailable-subtitle: The judiciary would never agree with us.

The courts agree that President [+ president +] is wrong to dismiss us, and that we should remain in the government. The President might respect the court's decision for now, but the fascists in the streets do not. They are still demanding snap elections or the resignation of the government, and will remain in the streets until we either meet their demands, or they are forced out.

- @new_elections
- @fight


@new_elections
title: Call for new snap elections.
subtitle: We will not resign at gunpoint, and we will attempt to maintain the integrity of the elections.
go-to: new_elections_braun if president == "Braun"; new_elections_hindenburg if president != "Braun" and pl2050_relation >= 40 and far_right_coalition < 50; new_elections_hindenburg_reject if president != "Braun" and (pl2050_relation < 40 or far_right_coalition >= 50)


@new_elections_braun
go-to: new_elections_braun_2 if konfederacja_normalized + pis_normalized >= 0.5 and far_right_coalition < 50; new_elections_braun_reject if (konfederacja_normalized + pis_normalized < 0.5 or far_right_coalition >= 50)

# if the Nazis + PiS have a current majority in the Reichstag, then they'll instantly launch the coup.


@new_elections_braun_reject

The fascists reject our offer of free and fair elections. They are demanding that President [+ president +] dismisses the current government and appoints Hitler prime_minister. [? if far_right_coalition >= 50 : They claim that since the far-right coalition has the majority in the Reichstag, they are constitutionally justified in appointing the chanellor. Unfortunately, their argument is not exactly wrong. ?]

- @braun_appoints_hitler
- @fight

@new_elections_braun_2
go-to: new_elections_2

The fascists seem to believe that they would win an election if one were to be held now, so they agree to our holding an election for next month.


@new_elections_hindenburg
go-to: new_elections_2

With the mediation of various establishment figures, new elections are called for next month, without the use of fascist "electoral monitors".


@new_elections_2
on-arrival: next_election_time = time + 1 if next_election_time > time+1; next_election_month = month + 1 if next_election_time >= time+1; next_election_year = year if next_election_time >= time+1; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12;


@new_elections_hindenburg_reject

Influenced by his inner circle and by the fascists in the streets, President Hindenburg rejects our offer of new free and fair elections, and again demands that we resign. There is no arguing.

- @resign
- @fight



@resign
title: Resign our positions in the government.
choose-if: president != "Braun"
unavailable-subtitle: President Braun cannot resign.
on-arrival: prime_minister = "Papen"; prime_minister_party = "I"; ko_in_government = 0;
go-to: accept

We have resigned our positions in the government.



@braun_appoints_hitler
title: We cannot do anything but appoint Hitler prime_minister.
view-if: president == "Braun"
choose-if: konfederacja_normalized >= 0.5 or largest_party == "Konfederacja"
unavailable-subtitle: There is no way President [+ president +] could do such a blatantly undemocratic act.
on-arrival: prime_minister = "Hitler"; prime_minister_party = "Konfederacja"; ko_in_government = 0

Threatened with a civil war, and unwilling to fight anymore, President [+ president +] dismisses the current government and appoints Hitler of the Konfederacja as prime_minister.

- @hitler_takes_power
