title: The Economic Crisis
new-page: true
is-card: true
view-if: crisis_urgency >= 1 and crisis_program_timer = 0 and economic_plan = 0 and black_thursday_seen == 1
frequency: 200
on-arrival: month_actions += 1; crisis_program_timer = 5
card-image: img/bankrun.jpg
tags: party_affairs, govt_affairs

= Addressing the Economic Crisis

Our party is wracked as to how to deal with the economic crisis. From the centrist faction, Hilferding argues that the economic crisis must be allowed to play out, because it is simply a part of the cyclic nature of capitalism.

From labor, the economist Wladimir Woytinsky argues that we must pursue a massive public works program to reduce unemployment and stimulate demand, perhaps including deficit spending. His opponents, like Hilferding, argue that this plan will cause a return to hyper-inflation, and that it would not even help the economy. In addition, according to Hilferding, Woytinsky's strategy is opposed to Marxist principles.

The so-called WTB plan, after its chief supporters Woytinsky, Tarnow, and Baade, is receiving more attention within the party.

However, leftists in the party argue that the WTB plan is merely a bandage over the fundamental flaws of capitalism. They argue that the crisis is an opportunity to end capitalism through massive nationalizations of industry, and finally bring our country to socialism.

Meanwhile, moderates and reformists draw up proposals for a limited plan of job creation through public works, funded through taxes, that does not involve deficit spending or large-scale restructuring of the economy. Labor argues that the plan is inadequate in its scale and ambition.

- @support_centrist: Support the Centrist view.
- @support_conservatives: Support the Labor view.
- @support_left: Support the Left view.
- @support_moderate: Support the Reformist view.
- @defer: Defer making a decision until we have more information.

@support_centrist
subtitle: Let the depression play itself out.
on-arrival: pragmatists_strength += 5; pragmatists_dissent -= 5; conservatives_dissent += 10; wtb_support -= 1; progressives_dissent += 10;

Woytinsky and the trade unions vehemently criticize the centrists for their policy. The workers are starving; are we going to callously abandon them and side with bourgeois austerity? On the other hand, the centrists criticize Woytinsky for promising the workers false hope; their position is that nothing can be done to alleviate the depression. They also speak of the fear of another round of hyperinflation that may result from deficit spending.

Our policy course is currently that nothing can be done to alleviate the depression.


@support_conservatives
subtitle: Call for increased public works and fiscal stimulus.
unavailable-subtitle: The Labor faction is not strong enough.
choose-if: historical_mode == 0 or (conservatives_strength >= progressives_strength and conservatives_strength >= pragmatists_strength and conservatives_strength >= reformist_strength)
on-arrival: pragmatists_dissent += 10; progressives_dissent += 5; conservatives_strength += 5; wtb_support += 1;
go-to: plan_adopted if wtb_support >= 3; plan_support if wtb_support < 3

@plan_support

Our support of the WTB plan alienates the orthodox Marxists of the Centrist faction, and faces both opposition and support from a variety of sources across the spectrum. Leftists argue that the plan is flawed because it leaves capitalism intact, while some reformists are cautiously optimistic.

We have not yet convinced the party to formally adopt the plan. More work needs to be done.

@plan_adopted
on-arrival: wtb_adopted = 1; conservatives_dissent -= 15; reformist_dissent -= 10; conservatives_strength += 10; pragmatists_strength -= 10; pragmatists_dissent += 20; progressives_dissent += 5; economic_plan = 1

With our hard work, we have finally convinced the majority of the party to adopt the WTB plan! [? if ko_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?][? if ko_in_government = 0 : We should emphasize the plan in our electoral propaganda. ?]

@support_left
subtitle: Call for mass nationalizations and the transformation of the economy.
unavailable-subtitle: The Left faction is not strong enough.
choose-if: historical_mode == 0 or (progressives_strength >= conservatives_strength and progressives_strength >= pragmatists_strength and progressives_strength >= reformist_strength)
on-arrival: progressives_strength += 10; reformist_dissent += 5; conservatives_dissent += 5; nationalization_support += 2
go-to: support_progressives_progress if nationalization_support < 3; adopt_left if nationalization_support >= 3

@support_progressives_progress

We attempt to convince the party to support a program of mass nationalizations in response to the economic crisis. Leftists argue that this policy is in accordance with our long-standing socialist platform, while reformists and some trade unionists argue that the plan is unrealistic, and that we need to focus on more immediate improvements to workers' lives.

We have not yet formally adopted the plan. More work needs to be done.

@adopt_left
on-arrival: progressives_strength += 15; progressives_dissent -= 10; reformist_dissent += 10; neorevisionist_dissent += 8; conservatives_dissent += 6; nationalization_adopted = 1; coup_progress += 1; economic_plan = 3; razem_relation += 5*(1-dissent)

With our hard work, we have convinced the majority of the party to adopt the socialist economic plan! [? if ko_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?] Bourgeois opposition is likely to be fierce. [? if ko_in_government == 0 : It might be difficult to enact the plan, given present conditions. ?]

@support_moderate
subtitle: Call for a limited program of job creation without deficit spending.
unavailable-subtitle: The Reformist faction is not strong enough.
choose-if: historical_mode == 0 or (reformist_strength >= progressives_strength and reformist_strength >= pragmatists_strength and reformist_strength >= conservatives_strength)
on-arrival: moderate_plan_support += 1; reformist_strength += 8; reformist_dissent -= 5; progressives_dissent += 6; conservatives_dissent += 5
go-to: adopt_moderate if moderate_plan_support >= 2; support_moderate_progress if moderate_plan_support < 2

@support_moderate_progress

We attempt to convince the party to support a moderate program of job creation without deficit spending. Advocates of the moderate plan argue that this plan is most likely to receive support from the bourgeois parties, and most practical in the immediate term. Opponents in the progressives and conservatives factions criticize the plan for being inadequate in its ambition.

We have not yet adopted the plan. More work needs to be done.

@adopt_moderate
on-arrival: reformist_strength += 10; reformist_dissent -= 10; progressives_dissent += 8; conservatives_dissent += 6; moderate_plan_adopted = 1; economic_plan = 2; coalition_dissent -= 1 if pensioners > 15; pl2050_relation += 5;

With our hard work, we have convinced the majority of the party to adopt the moderate job creation plan! [? if ko_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?]

@defer

We hold off on making a decision on economic policy, awaiting more information. Woytinsky and his supporters argue that inaction is a form of action of its own. 
