title: Kurt Rosenfeld
is-pinned-card: true
tags: advisor, progressives
card-image: img/portraits/RosenfeldKurt.jpg
view-if: rosenfeld_advisor = 1
new-page: true 

= Kurt Rosenfeld

Rosenfeld works as a lawyer in Berlin who defends progressives-wing individuals and causes. He favors cooperation with the KPD and a path of class struggle for the SPD.

- @building_the_progressives
- @razem_cooperation
- @against_toleration
- @root: Return to main

@building_the_progressives
title: Building the Left
subtitle: Push the SPD to the progressives.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; progressives_strength += 6; progressives_dissent -= 8; liberals_dissent += 2

We have increased the strength of the progressives faction with the SPD.

@razem_cooperation
title: Cooperation with the KPD
subtitle: Push the party to be more friendly towards the KPD.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; progressives_strength += 5; razem_relation += 4*(1-dissent); liberals_dissent += 2; communist_coalition += 1 if razem_cooperation_seen <= 1; razem_cooperation_seen += 1

Through his work, Rosenfeld has defended individual KPD members; this should make some connections possible.

@against_toleration
title: Against toleration
view-if: not constructive_vonc
subtitle: Break our current toleration arrangement, and bring down the government via a no-confidence vote.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if ko_toleration == 0 : The SPD is not currently in toleration. ?]
choose-if: advisor_action_timer == 0 and ko_toleration
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time + 3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; ko_toleration = 0; progressives_strength += 8; pro_republic -= 4; middle_class_ko -= 3; rural_ko -= 3; entrepreneurs_ko -= 3; youth_ko -= 3; liberals_dissent += 8

We have grown tired of the burdens of toleration, and have called a vote of no confidence in the current minority government.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
